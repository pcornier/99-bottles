
## Language SCELBAL ##
---
- Author: barrym
- Date: 08/14/10
- Info: http
- Score:  (3.00 in 13 votes)
- URL: n/a
- Original URL: http://www.99-bottles-of-beer.net/language-scelbal-2472.html
---

```SCR
1 REM BEER SONG IN SCELBAL (A SIMPLE DIALECT OF BASIC)
2 REM          BY BARRYM 2010-07-01
3 REM SCELBAL WAS AN ALTERNATIVE TO ASSEMBLY LANGUAGE ON
4 REM THE 8008 MACHINES OF THE EARLY TO MID 70S.  IT IS
5 REM QUITE SPARTAN, BUT ADEQUATE FOR SIMPLE JOBS.  BILL
6 REM GATES ALLEDGEDLY STATED THAT A BASIC INTERPRETER WAS
7 REM 'IMPOSSIBLE' TO IMPLEMENT ON AN 8008, BUT A MAN BY
8 REM THE NAME OF NAT WADSWORTH PROVED HIM WRONG IN 1975!
9 REM THANKS TO STEVE LOBOYKO FOR HIS SEMI-DECENT EMULATOR!
10 C=99
15 GOSUB 120
19 END
20 IF C=0 THEN 50
25 IF C<10 THEN 40
35 PRINT CHR(176+C/10);
40 PRINT CHR(176+C-10*INT(C/10));
45 GOTO 55
50 PRINT 'NO MORE';
55 PRINT ' BOTTLE';
60 IF C=1 THEN 70
65 PRINT 'S';
70 PRINT ' OF BEER';
75 IF D=0 THEN 85 
80 PRINT ' ON THE WALL';
85 D=D+1
90 IF D=0 THEN 99
95 PRINT '.'
99 RETURN
100 PRINT 'TAKE ONE DOWN AND PASS IT AROUND, ';
110 GOSUB 20
120 D=-1
130 PRINT
140 GOSUB 20
150 PRINT ', ';
160 GOSUB 20
170 C=C-1
180 IF C>=0 THEN 100
190 C=99
200 PRINT 'GO TO THE STORE AND BUY SOME MORE, ';
210 GOTO 20```