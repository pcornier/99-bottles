
## Language Befunge ##
---
- Author: Erefe
- Date: 09/23/07
- Info: http
- Score:  (3.15 in 97 votes)
- URL: n/a
- Original URL: http://www.99-bottles-of-beer.net/language-befunge-1602.html
---

```>#c101v //Funge 98, interpreted by ccbi.  Erefe 09/23/07    
v     <                                                     
>:!v>       :1`!v>               :2`!v>                  v  
 v$_^         v$_^                 v$_^                     
  -fxn 0----\  -fxn 1-in bool 1st\  -fxn 2--------------\   
 >20g'c-!#@_v >' *'N+20g:vv,_$  v| >020g1-4>j05s'_vv,_v |   
 \endif-----  |v;>.$    ;<>:^:<;v| |  rj0b"bottle"<>:^>$v   
              |>#^_\," erom o"^ >v \out bottle(s)-------    
              \out num of bttles-                           
v           <                    <                      <   
  v`6:   <v`5:    <v`4:                 <v`3:            <  
 v_$v    ^_$v     ^_$v                  ^_$v                
     fxn 6\  -fxn 5\  -fxn 4-------------\  -fxn 3---------\
    >a'.,,v >" ,",,v >0"llaw eht no ">:v | >0"reeb fo ">:v |
    \-----  \------  |               ^,_$v |           ^,_$v
                     \out "on the wall"--  \out "of beer"-- 
v         <        <                     <                 <
 >:7`!v>                             :8`!v>@                
    v$_^                               v$_^                 
     -fxn 7--------------------------\  fxn 8---------\     
    >20gv>0" ,erom emos yub dna ero"v| >20g1-'d+'d%20pv     
    |v0 _^          v"Go to the st" <| \dec number----      
    |>" ,dnuora ti"v                 |                      
    |v"n and pass "<v,_v             |                      
    |>"wod eno ekaT">:^>$            v                      
    \out "Take one..."||"buy more"...                       
v                                    <                <     
>1+:j vvvvvvvvvvvvvvvvv                                     
      0    1     1                                          
      1234512367812346>a,$0v                                
      v<<<<<<<<<<<<<<<     <```